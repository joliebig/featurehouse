/**
 * This testcase tests a bug in innermost fusion that occured in revision 13787.
 */
module test92

imports
  libstratego-lib
  Stratego

strategies

  /**
   * The bug results in: [Var("t"),Var(ListVar("t*"))]
   */
  main-test92 =
    !Match(AnnoList(Wld, [Var("t"), Var(ListVar("t*"))]))
    ; parenthesize-Stratego
    ; debug(!"result: ")
    ; ?Match(AnnoList(Wld, [Var("t"), Var(ListVar("t*"))]))

  parenthesize-Stratego =
    innermost(StrategoParenthesize)

rules

  StrategoParenthesize :
    Match(t_0) -> Match(Parenthetical(t_0))
    where <(?BA(_, _)
            + ?LChoice(_, _)
            + ?RChoice(_, _)
            + ?GuardedLChoice(_, _, _)
            + ?Choice(_, _)
            + ?DynRuleUnionFix(_, _)
            + ?DynRuleIntersectUnionFix(_, _, _)
            + ?DynRuleIntersectFix(_, _)
            + ?AM(_, _)
            + ?DynRuleIntersect(_, _, _)
            + ?DynRuleUnion(_, _, _)
            + ?DynRuleIntersectUnion(_, _, _, _)
            + ?Seq(_, _)
            + fail)> t_0

  StrategoParenthesize :
    AnnoList(t_0, t_1) -> AnnoList(Parenthetical(t_0), t_1)
    where <(?As(_, _) + fail)> t_0

  StrategoParenthesize :
    AnnoList(t_0, t_1) -> AnnoList(t_0, Parenthetical(t_1))
    where <(?As(_, _) + fail)> t_1

signature
  constructors
    Parenthetical : Unknown -> Unknown
