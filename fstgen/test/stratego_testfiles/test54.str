/**
 * This program illustrates an alternative transformation
 * scheme for the innermost strategy.
 * 
 * Note that the building and matching of the pair 
 * is assumed to be canceled by build/match fusion.
 */
module test54
imports libstratego-lib
signature
  constructors
    Nil   : List(a)
    Cons  : a * List(a) -> List(a)

    Zero  : Nat
    Succ  : Nat -> Nat
    Plus  : Nat * Nat -> Nat

rules

  A : Plus(Zero, x) -> x
  B : Plus(Succ(x), y) -> Plus(x, Succ(y))

strategies

  eval = 
    bottomup(try(AorB))

  AorB = 
    ?Plus(x, y)
    ; plus(|x, y)

  plus(|a, b) =
    !(a, b)
    ; ({x: ?(Zero, x); !x}
      <+ {x, y: ?(Succ(x), y); plus(|x, Succ(y))})
    
strategies

  main-test54 = 
    !Plus(Succ(Succ(Zero)), Succ(Zero));
    eval
