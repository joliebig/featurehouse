/**
 * this is a test for contextual rules, which are obsolete since
 * Stratego/XT 0.16
 */

module test44
imports libstratego-lib
strategies

  share(mkvar, always, mklet) = 
	graph(mkvar); 
	inline-graph(always, mklet)

strategies

  edge(mkvar)      = split(address; mkvar, all(address; mkvar))
  list-edge(mkvar) = split(address; mkvar, map(address; mkvar))

  graph(mkvar) = 
    rec x(is-list; split(list-edge(mkvar), map(x); unions); MkCons
                <+ split(edge(mkvar), \ _#(xs) -> xs\ ; map(x); unions); 
                   MkCons)

signature
  constructors
    GraphLet : List(Product([Int, Term])) * Term -> Term

strategies

  inline-graph(always, mklet) = 
	\ [(a, t) | graph] -> GraphLet(graph, t) \ ;
	repeat(
    	    inline; (GraphLet([(id,always) | id], id) + dead)
            <+ dead
            <+ dont-inline(mklet)
        );
	\ GraphLet([], t) -> t \

rules

  inline : 
	GraphLet([(a, skel) | graph], t[a]) ->
	GraphLet([(a, skel) | graph], t[skel])

  dead : 
	GraphLet([(a, skel) | graph], t) -> 
	GraphLet(graph, t)
	where <not(is-subterm)> (a, t)

  dont-inline(mklet) : 
	GraphLet([(a, skel) | graph], t) -> 
	GraphLet(graph, <mklet>(a, skel, t))

signature
  sorts A
  constructors
    F : A * A -> A
    A : A 
    Var : String -> A
    Let : String * A * A -> A

strategies

  main-test44 = 
    test-suite(!"share-test",
	test1;
	test2;
	test3;
	test4
    )

  test1 = 
    apply-and-check(!"test1"
	, share(\ x -> Var(x) \
               ,fail
	       ,\ (x, t, t') -> Let(x, t, t') \
	       )
	, !F(F(A, A), F(F(A, A), F(A, A)))
	, ?Let(Var(var1077592168),A,
           Let(Var(var1075859784),F(Var(var1077592168),Var(var1077592168)),
               F(Var(var1075859784),F(Var(var1075859784),Var(var1075859784)))))
	)

  test2 = 
    apply-and-check(!"test2"
	, graph(\ x -> Var(x) \ )
	, !F(F(A, A), F(F(A, A), F(A, A)))
	, ?[(Var(var1076162904),F(Var(var1076162872),Var(var1076162888))), 
	    (Var(var1076162888),F(Var(var1076162872),Var(var1076162872))), 
	    (Var(var1076162872),F(Var(var1077895264),Var(var1077895264))),
	    (Var(var1077895264),A)]
	)

  test3 = 
    apply-and-check(!"test3"
	, inline-graph(fail, \ (x, t, t') -> Let(x, t, t') \ )
	, ![(Var(1076162904),F(Var(1076162872),Var(1076162888))), 
	    (Var(1076162888),F(Var(1076162872),Var(1076162872))),
	    (Var(1076162872),F(Var(1077895264),Var(1077895264))), 
	    (Var(1077895264),A)]
	, ?Let(Var(var1077895264),A,
           Let(Var(var1076162872),F(Var(var1077895264),Var(var1077895264)),
               F(Var(var1076162872),F(Var(var1076162872),Var(var1076162872)))))
	)

  test4 = 
    apply-and-check(!"test4a"
	, inline
	, !GraphLet([(Var(1076162888),F(Var(1076162872),Var(1076162872))),
	    	     (Var(1076162872),F(Var(1077895264),Var(1077895264))), 
	    	     (Var(1077895264),A)], 
		    F(Var(1076162872),Var(1076162888)))
	, ?GraphLet([(Var(1076162888),F(Var(1076162872),Var(1076162872))),
		     (Var(1076162872),F(Var(1077895264),Var(1077895264))),
		     (Var(1077895264),A)],
		    F(Var(1076162872),F(Var(1076162872),Var(1076162872))))
	);
    apply-and-check(!"test4b"
	, inline
	, !GraphLet([(Var(1076162872),F(Var(1077895264),Var(1077895264))),
		     (Var(1077895264),A)],
		    F(Var(1076162872),F(Var(1076162872),Var(1076162872))))
	, ?GraphLet([(Var(1076162872),F(Var(1077895264),Var(1077895264))),
		     (Var(1077895264),A)],
	     F(F(Var(1077895264),Var(1077895264)),F(Var(1076162872),Var(1076162872))))

	)
