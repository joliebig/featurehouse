/**
 * This a test for issue STR-479
 */
module test93
imports
  libstratego-lib

strategies

  main-test93 =
      !Minus(
        And(
          CastPrim(Long,ExprName(Id("save")))
        , Lit(Hexa("0xffffffffL"))
        )
      , And(
          CastPrim(Long,ExprName(Id("borrow")))
        , Lit(Hexa("0xffffffffL"))
        )
      )
    ; parenthesize-Java-15
    ; say(!"------------------------------------------- ")
    ; debug
    ; say(!"------------------------------------------- ")
    ; ?Minus(
        Parenthetical(
          And(
            CastPrim(Long,ExprName(Id("save")))
          , Lit(Hexa("0xffffffffL"))
          )
        )
      , Parenthetical(
          And(
            CastPrim(Long,ExprName(Id("borrow")))
          , Lit(Hexa("0xffffffffL"))
          )
        )
      )

  parenthesize-Java-15 =
    innermost(Java-15Parenthesize)
  

rules

  /**
   * If this rule, which is not applicable, is removed, then the test
   * succeeds. This might suggest that the bug is an arity problem.
   */
  Java-15Parenthesize :
    Minus(t_0) -> Minus(Parenthetical(t_0))
    where <(?Minus(_, _)
            + ?And(_, _)
            + fail)> t_0

  Java-15Parenthesize :
    Minus(t_0, t_1) -> Minus(Parenthetical(t_0), t_1)
    where <(?And(_, _)
            + fail)> t_0

  Java-15Parenthesize :
    Minus(t_0, t_1) -> Minus(t_0, Parenthetical(t_1))
    where <(?And(_, _)
            + ?Minus(_, _)
            + fail)> t_1

signature
  constructors
    Parenthetical : Unknown -> Unknown

  constructors
    And                     : Expr * Expr -> Expr
    Minus                   : Expr * Expr -> Expr
    Minus                   : Expr -> Expr
    CastPrim                : PrimType * Expr -> Expr
    Lit                     : Literal -> Expr
    Long                    : IntType
    ExprName                : Id -> ExprName
    Hexa                    : HexaLiteral -> IntLiteral
                            : String -> HexaLiteral
    Id                      : ID -> Id
                            : String -> ID