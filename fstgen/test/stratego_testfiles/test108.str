/**
 * Test for rules ( := ) syntax
 */
module test108
imports libstratego-lib

strategies

  main-test108 = id
    ; not(Foo)

    ; {| Foo :
        rules(Foo := 2)
      ; Foo => 2
      |}

    ; not(Foo)

    ; {| Foo :
        rules(Foo := 2)
      ; Foo => 2
      ; rules(Foo := 3)
      ; Foo => 3
      |}

    ; not(Foo)

    /**
     * Global variables are scoped
     */
    ; {| Foo :
        rules(Foo := 2)
      ; Foo => 2
      ; {| Foo :
          rules(Foo := 3)
        ; Foo => 3
        |}
      ; Foo => 2
      |}

    /**
     * RHS is evaluated at definition time.
     */
    ; {| Foo, Bar :
        rules(Bar := "a")
      ; rules(Foo := <Bar>)
      ; rules(Bar := "b")
      ; Foo => "a"
      ; rules(Foo := <Bar>)
      ; Foo => "b"
      |}

    ; {| Foo :
        rules(Foo :+= 1)
      ; rules(Foo :+= 2)
      ; rules(Foo :+= 3)
      ; bagof-Foo => [3, 2, 1]
      |}

    /**
     * Multiple definition
     */
    ; {| Foo, Bar, Fred :
        rules(Bar := "a" Foo := 1 Fred := (2, "c"))
      ; Bar => "a"
      ; Foo => 1
      ; Fred => (2, "c")
      |}
